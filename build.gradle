plugins {
    id 'java-library'
    id 'io.github.goooler.shadow' version '8.1.7'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'com.diffplug.spotless' version '6.25.0'
}

group 'net.smoofyuniverse'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    spongevanilla
    spongeforge
}

minecraft {
    version '1.20.6'
}

dependencies {
    api 'org.spongepowered:spongeapi:11.0.0'

    implementation 'org.spongepowered:sponge:1.20.6-11.0.0:dev'
    implementation 'org.spongepowered:mixin:0.8.6'
    implementation 'org.ow2.asm:asm-util:9.7'

    implementation 'net.smoofyuniverse:oreupdater:1.3.1'
    implementation 'net.smoofyuniverse:worldmap:1.3.0'

    spongevanilla('org.spongepowered:spongevanilla:1.20.6-11.0.0:universal') { transitive = false }
    spongeforge('org.spongepowered:spongeforge:1.20.6-50.0.22-11.0.0:universal') { transitive = false }
}

jar {
    archiveClassifier = 'base'
}

shadowJar {
    archiveClassifier = ''

    dependencies {
        include dependency('net.smoofyuniverse:oreapi')
        include dependency('net.smoofyuniverse:oreupdater')
        include dependency('net.smoofyuniverse:worldmap')
    }

    relocate 'net.smoofyuniverse.ore', 'net.smoofyuniverse.superpiston.ore'
    relocate 'net.smoofyuniverse.map', 'net.smoofyuniverse.superpiston.map'

    manifest {
        attributes('MixinConfigs': 'mixins.superpiston.json')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "utf-8"
}

tasks.register('setupVanillaServer', Copy) {
    into 'run/vanilla'

    from configurations.spongevanilla
    rename('spongevanilla-(.*).jar', 'spongevanilla.jar')

    into('mods') {
        from shadowJar
    }
}

tasks.register('setupForgeServer', Copy) {
    into 'run/forge/mods'

    from configurations.spongeforge
    rename('spongeforge-(.*).jar', 'spongeforge.jar')

    from shadowJar
}


spotless {
    ratchetFrom 'origin/master'

    java {
        licenseHeaderFile(rootProject.file('HEADER'))
    }
}
